%{
#include "k-utils.h"
#include "molokai.h"
#include "y.tab.h"
  
u_long yyline=1;
void ReportError(const char *s);
%}

D     [0-9]
L     [a-zA-Z_]
E     [Ee][+-]?{D}+

%%

"//".* 
{D}*"."{D}+({E})? { 
                    yylval.line=yyline; 
                    char *a; yylval.num=strtod(yytext, &a); 
                    return NUMBER;
                  }
{D}+"."{D}*({E})? {
                    yylval.line=yyline; 
                    char *a; yylval.num=strtod(yytext, &a); 
                    return NUMBER;
                  }
{D}+ {
       yylval.line=yyline; 
       char *a; yylval.cnt=strtoul(yytext, &a, 0); 
       return COUNT;
     }
"number" {return NUM_TYPE;}
"counter" {return COUNT_TYPE;}
"construct" {return CONSTRUCT_TYPE;}
"load" {return LOAD;}
"x_coord" {return X;}
"y_coord" {return Y;}
"z_coord" {return Z;}
"width" {return W;}
"length" {return L;}
"height" {return H;}
"if" {return IF;}
"else" {return ELSE;}
"while" {return WHILE;}
"break" {return BREAK;}
"continue" {return CONTINUE;}
"maybe"|"or_maybe" {return MAYBE;}
"otherwise" {return OTHERWISE;}
"rotate" {return ROTATE;}
"move" {return MOVE;}
"scale" {return SCALE;}
"collides" {return COLLIDES;}
"print" {return PRINT;}
"printmap" {return PRINTMAP;}
"render" {return RENDER;}
"build" {return BUILD;}
"->" {return UPTO;}
">=" {return GTE;}
"<=" {return LTE;}
"==" {return EQ;}
"!=" {return NE;}
"&&" {return LAND;}
"||" {return LOR;}
{L}({D}|{L})* {yylval.line=yyline; strcpy(yylval.id, yytext); return ID;}

[ \t] {} 
[\n] {yyline++;}

. {return yytext[0];}

%%

void ReportError(const char *s)
{
  fprintf(stderr, "Error Line %d: %s\n", (int)yyline, s);
}

int yywrap(void){return 1;}
